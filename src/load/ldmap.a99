;##############################################################################;
; ldmap.a99 - contains code that loads a map from a given bank                 ;
;==============================================================================;
; This file is a part of VePseu 99!                                             ;
;                                                                              ;
; Copyright (c) 2020, Charles Thompson. All rights reserved.                   ;
;                                                                              ;
; Redistribution and use in source and binary forms, with or without           ;
; modification, are permitted provided that the following conditions are met:   ;
;                                                                              ;
; 1. Redistributions of source code must retain the above copyright notice,    ;
;    this list of conditions and the following disclaimer.                     ;
; 2. Redistributions in binary form must reproduce the above copyright notice, ;
;    this list of conditions and the following disclaimer in the documentation ;
;    and/or other materials provided with the distribution.                    ;
; 3. Neither the name of the copyright holder nor the names of its             ;
;    contributors may be used to endorse or promote products derived from this ;
;    software without specific prior written permission.                        ;
;                                                                              ;
; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"  ;
; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE    ;
; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE   ;
; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE    ;
; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR          ;
; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF         ;
; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS     ;
; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN      ;
; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)      ;
; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE   ;
; POSSIBILITY OF SUCH DAMAGE.                                                  ;
;##############################################################################;

; Length of the map in bytes
MAPLEN  EQU >100

    EVEN

; LDMAP - loads a map from a bank
; A5 = map number
LDMAP
; Load the map's bank into memory
; 102,562 cycles in total
    MOV RTADDR, A8 ; 14 cycles
    .SRB1 ; 10 cycles
    BL @LDBANK ; 20 + 102,494 cycles
    MOV A8, RTADDR ; 14 cycles
; Copy the map from RAM bank 2 to bank 1
; This is done to simplify render code, since the screen buffer is on bank 1
; If we kept the map on bank 2 things would slow down quite a bit, and at the
; cost of only about 256 bytes of ram
    MOV @MAPPTR, A5
    CLR PX
    CLR PY
    CLR PA
; Set PX, PY, and PA before the move
    MOVB *A5+, PX
    SWPB PX
    MOVB *A5+, PY
    SWPB PY
    MOVB *A5+, PA
    SWPB PA
    LI A6, RAMMAP
    LI A7, MAPLEN
LDML1
; Swap to bank 2, get a byte, swap to bank 1, store a byte...
    MOVB *A5+, A8
    .SRB0
    MOVB A8, *A6+
    DEC A7
    .SRB1
    JNE LDML1

; Set MAP to point to RAMMAP
    LI MAP, RAMMAP

; Set MUSI to CURMUS
    MOV @CURMUS, @MUSI
; Set the duration to 1
    LI A1, >0100
    MOVB A1, @MUSD
    .SRB0

; Set the default notification
    LI A1, DNOTIF
    MOV A1, @NOTIFY

; Set the delay to 100ms
    CLR CRU
    SBO 0
    LI A1, 4694
    INCT CRU
    LDCR A1, 14
    DECT CRU
    SBZ 0

    RT
