;##############################################################################;
; ldclr.a99 - contains code that loads the color table                         ;
;==============================================================================;
; This file is a part of VePseu 99!                                             ;
;                                                                              ;
; Copyright (c) 2020, Charles Thompson. All rights reserved.                   ;
;                                                                              ;
; Redistribution and use in source and binary forms, with or without           ;
; modification, are permitted provided that the following conditions are met:   ;
;                                                                              ;
; 1. Redistributions of source code must retain the above copyright notice,    ;
;    this list of conditions and the following disclaimer.                     ;
; 2. Redistributions in binary form must reproduce the above copyright notice, ;
;    this list of conditions and the following disclaimer in the documentation ;
;    and/or other materials provided with the distribution.                    ;
; 3. Neither the name of the copyright holder nor the names of its             ;
;    contributors may be used to endorse or promote products derived from this ;
;    software without specific prior written permission.                        ;
;                                                                              ;
; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"  ;
; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE    ;
; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE   ;
; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE    ;
; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR          ;
; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF         ;
; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS     ;
; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN      ;
; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)      ;
; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE   ;
; POSSIBILITY OF SUCH DAMAGE.                                                  ;
;##############################################################################;

    EVEN

; Load the color table onto the VDP
; Takes 1662 cycles in total!
LDCLR
; Initialize the loop
; 166 cycles
    MOV RTADDR, A2 ; 14 cycles
; Set the write address to VDPCTA
    LI A5, VDPCTA ; 12 cycles
    BL @VDCSWA ; 20 cycles + 120 cycles
; First make all the text white on black (the first 16 color entries)
; 24 cycles
    LI A1, >F100 ; 12 cycles
    LI A3, >10 ; 12 cycles
; Load the white-on-black color pallet into ram
; 16 loops, 670 cycles in total
LDCL1
    MOVB A1, @VWRIT; 22 cycles
    DEC A3 ; 10 cycles
    JNE LDCL1 ; 10 cycles, -2 at the end of the loop

; Initialize the loop
; 24 cycles
    LI A1, CLRTBL ; 12 cycles
    LI A3, >10 ; 12 cycles
; Load the color pallet onto the vdp
; 16 loops, 766 cycles in total
LDCL2
    MOVB *A1+, @VWRIT ; 28 cycles
    DEC A3 ; 10 cycles
    JNE LDCL2 ; 10 cycles, -2 at the end of the loop
; Return! 12 cycles
    B *A2
