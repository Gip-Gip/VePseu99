;##############################################################################;
; music.a99 - plays level music                                                ;
;==============================================================================;
; This file is a part of VePseu 99!                                            ;
;                                                                              ;
; Copyright (c) 2020, Charles Thompson. All rights reserved.                   ;
;                                                                              ;
; Redistribution and use in source and binary forms, with or without           ;
; modification, are permitted provided that the following conditions are met:  ;
;                                                                              ;
; 1. Redistributions of source code must retain the above copyright notice,    ;
;    this list of conditions and the following disclaimer.                     ;
; 2. Redistributions in binary form must reproduce the above copyright notice, ;
;    this list of conditions and the following disclaimer in the documentation ;
;    and/or other materials provided with the distribution.                    ;
; 3. Neither the name of the copyright holder nor the names of its             ;
;    contributors may be used to endorse or promote products derived from this ;
;    software without specific prior written permission.                       ;
;                                                                              ;
; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"  ;
; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE    ;
; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE   ;
; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE    ;
; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR          ;
; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF         ;
; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS     ;
; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN      ;
; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)      ;
; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE   ;
; POSSIBILITY OF SUCH DAMAGE.                                                  ;
;##############################################################################;

NOISB EQU >E600
AUDO1 EQU >8000
AUDO2 EQU >A000
SADDR EQU >8400

    EVEN

MUSIC
; If we're still playing the last note, don't do anyhting
    CLR A1
    MOVB @MUSD, A1
    CI A1, >0100
    JNE MUSEND
    .SRB1
    MOV @MUSI, A1
    MOVB *A1+, @MUSD
; If the duration is 0, reset the song
    JEQ MUSB1
MUSB1R
    LI A2, SADDR
; Store the c0 volume to the noise channel
; Load the default noise address/mode byte into A3
    LI A3, NOISB
; Send that byte to the sound address
    MOVB A3, *A2
; Send the attenuation of the noise address to the sound address
    MOVB *A1+, *A2
; Store the c1 volume and note to the 1st square wave channel
    CLR A3
    MOVB *A1+, A3
    SWPB A3
    SLA A3, 1
    AI A3, NOTES
    MOV *A3, A4
    ORI A4, AUDO1
    MOVB A4, *A2
    SWPB A4
    MOVB A4, *A2
    MOVB *A1+, *A2
; Store the c2 volume and note to the 2nd square wave channel
    CLR A3
    MOVB *A1+, A3
    SWPB A3
    SLA A3, 1
    AI A3, NOTES
    MOV *A3, A4
    ORI A4, AUDO2
    MOVB A4, *A2
    SWPB A4
    MOVB A4, *A2
    MOVB *A1+, *A2
    .SRB0
    MOV A1, @MUSI
    RT

MUSB1
    MOV @CURMUS, A1
    MOVB *A1+, @MUSD
    JMP MUSB1R

MUSEND
    AI A1, ->0100
    MOV A1, @MUSD
    RT
